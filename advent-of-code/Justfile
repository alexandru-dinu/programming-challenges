set shell := ["bash", "-c"]

# Create a new day directory from template and fetch input
new year day:
    #!/usr/bin/env bash
    set -e

    TARGET="{{year}}/{{day}}"
    mkdir -p "$TARGET"
    cp -r .template/ "$TARGET/"

    . .env && ./.scripts/get_input.sh {{year}} {{day}} > "$TARGET/input"

    TITLE=$(curl -s "https://adventofcode.com/{{year}}/day/{{day}}" \
            | rg -e '--- Day \d+:\s*(.*?)\s*---' -o \
            | tr -d '-' \
            | xargs)
    echo "--- $TITLE ---"

    cat <<EOF > "$TARGET/README.md"
    ---
    title: "$TITLE"
    url: https://adventofcode.com/{{year}}/day/{{day}}
    tags: ...
    ---

    # Solution
    ...

    # Usage
    \`\`\`
    $ make
    \`\`\`
    EOF

# Update badges in README.md
badges:
    . ./.env && uv tool run --with requests mdup -i README.md && sed -i '' -e '/`/d' README.md

# List tags from README.md frontmatter
tags:
    @./.scripts/frontmatter.py -f $(fd -E '.*' -e md)

# Remove cache directories
clean:
    find . -name "__pycache__" -print0 | xargs -0 rm -rf
    find . -name "__coconut_cache__" -print0 | xargs -0 rm -rf
    find . -name ".hypothesis" -print0 | xargs -0 rm -rf
    find . -name ".pytest_cache" -print0 | xargs -0 rm -rf
    find . -name ".ipynb_checkpoints" -print0 | xargs -0 rm -rf
    find . -name ".mypy_cache" -print0 | xargs -0 rm -rf

# Format Python files
format-py:
    #!/usr/bin/env bash
    src_py=$(fd -E '.*' -e py)
    uv tool run autoflake --remove-all-unused-imports -i $src_py && \
        uv tool run isort $src_py && \
        uv tool run black --line-length=88 $src_py

# Run tests in nested Makefiles
test:
    #!/usr/bin/env bash
    test_mk=$(find . -mindepth 2 -not -path '*/\.*' -name "Makefile" -exec grep -l "^test:" {} \; | xargs -n 1 dirname)
    for mk in $test_mk; do
        make -C $mk test || exit 1
    done
